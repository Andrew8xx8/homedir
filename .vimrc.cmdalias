let loaded_cmdalias = 200

" Make sure line-continuations won't cause any problem. This will be restored
"   at the end
let s:save_cpo = &cpo
set cpo&vim

" Define a new command alias.
function! CmdAlias(lhs, rhs, ...)
  if a:0 > 0
    let flags = a:1.' '
  else
    let flags = ''
  endif
  exec 'cnoreabbr <expr> '.flags.a:lhs.
	\ " <SID>ExpandAlias('".a:lhs."', '".a:rhs."')"
endfunction

function! s:ExpandAlias(lhs, rhs)
  if getcmdtype() == ":"
    " Determine if we are at the start of the command-line.
    " getcmdpos() is 1-based.
    let firstWord = strpart(getcmdline(), 0, getcmdpos())
    if firstWord == a:lhs
      return a:rhs
    endif
  endif
  return a:lhs
endfunction

" Restore cpo.
let &cpo = s:save_cpo
unlet s:save_cpo


call CmdAlias('hs', 'Moccur')
call CmdAlias('pst', 'set paste')
call CmdAlias('npst', 'set nopaste')
call CmdAlias('sso', 'SessionOpen')
call CmdAlias('sss', 'SessionSave')
call CmdAlias('ssa', 'SessionSaveAs')
